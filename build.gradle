apply plugin: 'java'
apply plugin: 'groovy'

//build stuff
sourceCompatibility = 1.6
targetCompatibility = 1.6

def cucumberVersion = "1.2.4"

repositories {
    mavenCentral()
}

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

task cucumber() {
    dependsOn assemble
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime
            args = ['--plugin', 'pretty', '--glue', 'src/test/groovy', 'src/test/resources']

        }
    }
}

//Ensure the cucumber tests are executed as part of the build. Makes for a very pretty output.
build.dependsOn cucumber

dependencies {
    // Groovy library for groovy building!
    compile 'org.codehaus.groovy:groovy-all:2.4.3'

    /*
   In order to work around a really flagrant bug ( http://issues.gradle.org/browse/GRADLE-732 )
   You have to create a configuration that includes the jar.archivePath (the jar.archivePath is created by the java plugin)
   and give it the same name as the cucumberRuntime configuration, or name it something different and have the cucumberRuntime
   configuration extend from it as well.

    Still a bug in gradle 2.4, despite their saying that they would fix it before 1.0

   VERY ANNOYING BUG
    */
    cucumberRuntime files("${jar.archivePath}")

    testCompile 'junit:junit:4.11'
    testCompile "info.cukes:cucumber-junit:$cucumberVersion"
    testCompile "info.cukes:cucumber-groovy:$cucumberVersion"
}

